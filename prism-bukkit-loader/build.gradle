plugins {
    id 'prism.java-conventions'
    id 'com.gradleup.shadow' version '9.0.0-beta11'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'xyz.jpenilla.resource-factory-bukkit-convention' version '1.2.0'
}

dependencies {
    compileOnly project.property('deps.log4j-api')
    compileOnly project.property('deps.paper-api')

    implementation(project.property('deps.triumph-gui')) {
        exclude(group: "net.kyori")
    }

    implementation project(':prism-loader')
}

shadowJar {
    archiveFileName = "prism-paper-${project.ext.fullVersion}.jar"

    from {
        project(':prism-bukkit').tasks.shadowJar.archiveFile
    }

    relocate 'com.typesafe', 'network.darkhelmet.prism.libs.typesafe'
    relocate 'com.fasterxml.jackson', 'network.darkhelmet.prism.libs.jackson'
    relocate 'dev.triumphteam', 'network.darkhelmet.prism.libs.triumphteam'
    relocate 'net.kyori.moonshine', 'network.darkhelmet.prism.libs.kyori.moonshine'
    relocate 'org.objectweb.asm', 'network.darkhelmet.prism.asm'
    relocate 'org.spongepowered.configurate', 'network.darkhelmet.prism.libs.configurate'
    relocate 'io.leangen.geantyref', 'network.darkhelmet.prism.libs.geantyref'
}

artifacts {
    archives shadowJar
}

runServer {
    minecraftVersion(project.property('paper-api-version'))
}

bukkitPluginYaml {
    name.set('prism')
    version.set(project.property('version'))
    main.set('network.darkhelmet.prism.bukkit.loader.PrismBukkitPluginLoader')
    apiVersion.set(project.property('paper-api-version'))
    website.set(project.property('website'))
    authors.add("viveleroi")
    depend.add("NBTAPI")
    softDepend.add("WorldEdit")
    load.set(xyz.jpenilla.resourcefactory.bukkit.BukkitPluginYaml.PluginLoadOrder.STARTUP)
}
